import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: com.smokejumperit.gradle.OneJarPlugin

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

project.sourceCompatibility = 1.7
project.targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile ("com.yammer.dropwizard:dropwizard-core:0.6.2") {
        exclude group: 'org.eclipse.jetty.orbit'
    }
    compile "org.apache.geronimo.specs:geronimo-servlet_3.0_spec:1.0"
    compile 'org.projectlombok:lombok:0.11.6'
    compile "org.springframework:spring-web:$springVersion"
    compile "javax.inject:javax.inject:1"

    compile "org.springframework.security:spring-security-web:$springSecurityVersion"
    compile "org.springframework.security:spring-security-config:$springSecurityVersion"
    compile "org.springframework.security:spring-security-aspects:$springSecurityVersion"

}


jar {
    manifest {
        attributes 'Main-Class': 'com.github.jacek99.myapp.MyAppService'
    }
}

/**
 * One-JAR plugin integration
 */
buildscript {
    repositories {
        mavenRepo url:'http://repo.smokejumperit.com'
        maven {
            name 'Gradle Shadow'
            url 'http://dl.bintray.com/content/johnrengelman/gradle-plugins'
        }
    }
    dependencies {
        classpath 'com.smokejumperit:gradle-plugins:0.8.2'
        classpath 'org.gradle.plugins:shadow:0.7.4'
    }
}

apply plugin: 'shadow'

task run(dependsOn: 'classes', type: JavaExec) {
    main = 'com.github.jacek99.myapp.MyAppService'
    classpath = sourceSets.main.runtimeClasspath
    args 'server','myapp.yml'
}

//deprecated
task runOneJar(type:Exec, dependsOn: 'oneJar') {
    workingDir = "build/libs"
    commandLine = ['java', '-jar', '-server', "dropwizard-spring-di-security-onejar-example-$version-standalone.jar",'server','../../myapp.yml']
}

task runShadow(type:Exec, dependsOn: 'shadow') {
    workingDir = "build/libs"
    commandLine = ['java', '-jar', '-server', "dropwizard-spring-di-security-onejar-example-$version-shadow.jar",'server','../../myapp.yml']
}

task bdd(type: Exec, dependsOn: 'shadow') {
    description = "Tests BDDs against the running application"
    
    // fix to run cucumber on a particular infernal OS properly
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', 'bundle.bat', 'exec', 'cucumber'
    }
    else {
        commandLine = ['cucumber']
    }
    workingDir = "./src/test/resources/bdd" 
    def oneJarProcess = null

    doFirst{
        println "Starting application on separate thread..."
        Thread.startDaemon {
            oneJarProcess = "java -jar -server build/libs/dropwizard-spring-di-security-onejar-example-$version-shadow.jar server myapp.yml".execute()
        }
        println "Waiting for application to start before executing tests..."
        addShutdownHook {
            println "Shutting down application"
            oneJarProcess.destroy()
        }
    }

}
